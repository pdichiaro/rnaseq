def generateStarAlignArgs(save_unaligned, contaminant_screening, extra_star_align_args, quantifier = 'salmon') {
    def argsToMap = { String args ->
        args.split(/\s(?=--)/).collectEntries {
            def parts = it.trim().split(/\s+/, 2)
            [(parts[0]): parts.size() > 1 ? parts[1] : '']
        }
    }

    // Common args between pipeline and RSEM
    def base_args = """
        --quantMode TranscriptomeSAM
        --outSAMtype BAM Unsorted
        --outSAMattributes NH HI AS NM MD
        --readFilesCommand zcat
    """.trim()

    // Add quantifier-specific args
    if (quantifier == 'rsem') {
        // RSEM-compatible configuration
        base_args += " " + """
            --outSAMunmapped Within
            --outFilterType BySJout
            --outFilterMultimapNmax 20
            --outFilterMismatchNmax 999
            --outFilterMismatchNoverLmax 0.04
            --alignIntronMin 20
            --alignIntronMax 1000000
            --alignMatesGapMax 1000000
            --alignSJoverhangMin 8
            --alignSJDBoverhangMin 1
            --sjdbScore 1
        """.trim()
    } else {
        // Standard pipeline configuration
        base_args += " " + """
            --twopassMode Basic
            --runRNGseed 0
            --outFilterMultimapNmax 20
            --alignSJDBoverhangMin 1
            --outSAMstrandField intronMotif
        """.trim()
    }

    if (save_unaligned || contaminant_screening) {
        base_args += " --outReadsUnmapped Fastx"
    }

    def final_args_map = argsToMap(base_args) + (extra_star_align_args ? argsToMap(extra_star_align_args) : [:])
    final_args_map.collect { key, value -> "${key} ${value}".trim() }.join(' ')
}

if (!params.skip_alignment && (params.aligner == 'star_salmon' || params.aligner == 'star_rsem')) {
    process {

        // We have to condition this, because the args are slightly different between the latest STAR and the one compatible with iGenomes
        // The Sentieon implementation also uses the older arguments version

        withName: '.*:ALIGN_STAR:STAR_ALIGN' {
            ext.args   = {
                def quantifier = params.aligner == 'star_rsem' ? 'rsem' : 'salmon'
                def extra_args = params.extra_star_align_args ?: ''
                if (quantifier == 'salmon') {
                    extra_args += ' --quantTranscriptomeSAMoutput BanSingleEnd'
                }
                generateStarAlignArgs(
                    params.save_unaligned,
                    params.contaminant_screening,
                    extra_args,
                    quantifier
                )
            }
        }
        withName: '.*:ALIGN_STAR:STAR_ALIGN_IGENOMES|.*:ALIGN_STAR:SENTIEON_STAR_ALIGN' {
            ext.args   = {
                def quantifier = params.aligner == 'star_rsem' ? 'rsem' : 'salmon'
                def extra_args = params.extra_star_align_args ?: ''
                if (quantifier == 'salmon') {
                    extra_args += ' --quantTranscriptomeBan Singleend'
                }
                generateStarAlignArgs(
                    params.save_unaligned,
                    params.contaminant_screening,
                    extra_args,
                    quantifier
                )
            }
        }
        withName: '.*:ALIGN_STAR:STAR_ALIGN|.*:ALIGN_STAR:STAR_ALIGN_IGENOMES|.*:ALIGN_STAR:SENTIEON_STAR_ALIGN' {

            publishDir = [
                [
                    path: { "${params.outdir}/${params.aligner}/log" },
                    mode: params.publish_dir_mode,
                    pattern: '*.{out,tab}'
                ],
                [
                    path: { params.save_align_intermeds ? "${params.outdir}/${params.aligner}" : params.outdir },
                    mode: params.publish_dir_mode,
                    pattern: '*.bam',
                    saveAs: { params.save_align_intermeds ? it : null }
                ],
                [
                    path: { params.save_unaligned ? "${params.outdir}/${params.aligner}/unmapped" : params.outdir },
                    mode: params.publish_dir_mode,
                    pattern: '*.fastq.gz',
                    saveAs: { params.save_unaligned ? it : null }
                ]
            ]
        }
    }
}
